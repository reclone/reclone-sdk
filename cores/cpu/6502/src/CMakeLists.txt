
add_custom_command (
  OUTPUT  VCpu6502.cpp
          VCpu6502.h
          VCpu6502__Syms.cpp
          VCpu6502__Syms.h
          VCpu6502__Trace.cpp
          VCpu6502__Trace__Slow.cpp
  DEPENDS Cpu6502.v
          Cpu6502MicrocodeRom.v
          Cpu6502Alu.v
          Cpu6502Opcodes.vh
          Cpu6502MicrocodeConstants.vh
  COMMAND perl ${VERILATOR} ${VERILATOR_OPTS} -I${CMAKE_CURRENT_SOURCE_DIR} -Mdir ${CMAKE_CURRENT_BINARY_DIR} -cc
    ${CMAKE_CURRENT_SOURCE_DIR}/Cpu6502.v
    ${CMAKE_CURRENT_SOURCE_DIR}/Cpu6502MicrocodeRom.v
    ${CMAKE_CURRENT_SOURCE_DIR}/Cpu6502Alu.v
)

# Verilog .v files are added here so that they get added to any IDE project generated by CMake.
add_library (cpu6502 STATIC
    VCpu6502.cpp
    VCpu6502__Syms.cpp
    VCpu6502__Trace.cpp
    VCpu6502__Trace__Slow.cpp
    Cpu6502.v
    Cpu6502MicrocodeRom.v
    Cpu6502Alu.v
    Cpu6502Opcodes.vh
    Cpu6502MicrocodeConstants.vh
)

target_compile_options(cpu6502 PRIVATE ${VK_CPPFLAGS})

target_include_directories(cpu6502 PUBLIC ${CMAKE_CURRENT_BINARY_DIR})

target_link_libraries(cpu6502 verilated)
